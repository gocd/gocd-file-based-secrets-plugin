/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "me.champeau.jmh" version "0.7.3"
}

configurations {
    extractedAtTopLevel
}

ext {
    deps = [
        gocdPluginApi: 'cd.go.plugin:go-plugin-api:25.1.0',
        lombok       : 'org.projectlombok:lombok:1.18.36',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
    compileOnly project.deps.gocdPluginApi
    implementation group: 'cd.go.plugin.base', name: 'gocd-plugin-base', version: '1.0.0'

    implementation project(':db')
    implementation project(':cli')

    compileOnly project.deps.lombok
    annotationProcessor project.deps.lombok

    extractedAtTopLevel project(':jar-class-loader')

    testCompileOnly project.deps.gocdPluginApi
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.15.2'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.3'
}

jar {
    dependsOn project.configurations.extractedAtTopLevel

    manifest {
        attributes 'Main-Class': 'com.thoughtworks.gocd.Boot'
        attributes 'GoCD-Main-Class': 'cd.go.plugin.secret.filebased.cli.Main'
    }

    from(configurations.runtimeClasspath) {
        into "lib/"
    }

//    This is useful for debugging
    from(sourceSets.main.java) {
        into "/"
    }

    from(project.configurations.extractedAtTopLevel.collect { it.isDirectory() ? it : zipTree(it) }) {
        into("/")
    }
}

test {
    useJUnitPlatform()
}
