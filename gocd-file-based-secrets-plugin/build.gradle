/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "me.champeau.gradle.jmh" version "0.5.3"
}

configurations {
    extractedAtTopLevel
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '22.2.0'
    implementation group: 'cd.go.plugin.base', name: 'gocd-plugin-base', version: '0.0.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

    implementation project(':db')
    implementation project(':cli')

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.24'

    extractedAtTopLevel project(':jar-class-loader')

    testCompileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '22.1.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.6.1'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
}

jar {
    dependsOn project.configurations.extractedAtTopLevel

    manifest {
        attributes 'Main-Class': 'com.thoughtworks.gocd.Boot'
        attributes 'GoCD-Main-Class': 'cd.go.plugin.secret.filebased.cli.Main'
    }

    from(configurations.runtimeClasspath) {
        into "lib/"
    }

//    This is useful for debugging
    from(sourceSets.main.java) {
        into "/"
    }

    from(project.configurations.extractedAtTopLevel.collect { it.isDirectory() ? it : zipTree(it) }) {
        into("/")
    }
}

test {
    useJUnitPlatform()
}
